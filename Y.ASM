; YOGO 02-01-2021
	
; ASCII CHARACTERS
CR		EQU		0DH
LF		EQU		0AH
CTRLZ	EQU		1AH
ESC		EQU		27
OPENB	EQU		'['
SCOLON	EQU		';'
PWN		EQU		'I'
HOL		EQU		'.'

MINUSONE	EQU	255


; CP/M BDOS FUNCTIONS
RCONF	EQU		1
WCONF	EQU		2
CONIOF	EQU		6


; CP/M ADDRESSES
RBOOT	EQU		0
BDOS	EQU		5
TPA		EQU		100H

MAIN:	ORG		TPA			; CREATE STACK SPACE
		LXI		SP,STACK
		CALL	CLS
		CALL	SETUP
		CALL	PRTBORD
MA01:	CALL	SETMOVPTR   ; SET MOVEPRT TO NEXT MOVE TO TRY
		CALL	FINDMOVE
		JNZ		MA02		; NO MOVE FOUND
		CALL	DOMOVE
		JMP		MA01	
MA02:	CALL	BACKUP
		JMP		MA01
		

DISMOV:
		LDA		MOVENO		; DISPLAY THE MOVE
		INR		A
		CALL	BIN2ASC
		MOV		A,B
		CPI		'0'
		JNZ		DISM01
		MVI		A,' '
DISM01:	STA		MTENS
		MOV		A,C
		STA		MONES
		CALL	SETCOORD
		CALL	SETMCUR
		LXI		H,OUTMVBUF
		CALL	NPRINT
		RET

SETCOORD:
		LXI		B,0000H
		LXI		D,0000H
		LDA		MOVEPTR
		MOV		C,A			; BC HOLDS MOVEPTR
		LXI		H,JUMP
		DAD		B
		MOV		E,M			; DE POINTS TO BORD ENTRY
		LXI		H,COLPOS
		DAD		D
		MOV		C,M
		LXI		H,DFOOT+2
		DAD		B
		MOV		A,M
		STA		JLET
;
		LXI		H,LINPOS
		DAD		D
		MOV		A,M
		ADI		'0'
		STA		JNUM
;		
		LDA		MOVEPTR
		MOV		C,A			; BC HOLDS MOVEPTR
		LXI		H,LAND
		DAD		B
		MOV		E,M			; DE POINTS TO BORD ENTRY
		LXI		H,COLPOS
		DAD		D
		MOV		C,M
		LXI		H,DFOOT+2
		DAD		B
		MOV		A,M
		STA		LLET
;
		LXI		H,LINPOS
		DAD		D
		MOV		A,M
		ADI		'0'
		STA		LNUM		
		RET
				
		
SETMCUR:
		MVI		E,30
		LDA		MOVENO
		INR		A
		CPI		17
		JM		SETM01
		SUI		16
		MVI		E,45
SETM01:	MOV		D,A
		CALL	SETCUR
		RET
				
BACKUP:
		MVI		B,0
		LDA		MOVENO
		MOV		C,A
		LXI		H,MOVEBUF
		DAD		B
		MVI		A,MINUSONE
		MOV		M,A			; SET START OF SEARCH TO -1
							; PRIOR TO SEARCH IT WILL BE
							; INCREMENTED
		DCX		H
		MOV		A,M
		STA		MOVEPTR
		CALL	UNMOVE
		RET
				
SETMOVPTR:
		LXI		H,MOVEBUF
		MVI		B,0
		LDA		MOVENO
		MOV		C,A
		DAD		B
		MOV		A,M
		INR		A
		STA		MOVEPTR
		RET
		
;
; SET ALL MOVEPTRS TO MINUS ONE
; AND MOVEPTR TO ZERO
;
SETUP:	MVI		C,33
		LXI		H,MOVEBUF
		MVI		A,MINUSONE	; SET DEFAULT MOVEPRT TO -1							
SET01:	MOV		M,A			; WILL BE INCREMENTED AT EACH SERACH
		INX		H
		DCR		C
		JNZ		SET01
		XRA		A
		STA		MOVENO		; SET CURRENT MOVE TO ZERO
		RET		
					
NPRINT:	MOV		C,M 		; GET THE NUMBER OF CHARS TO PRINT
NPR000: INX		H
		MOV		A,M
		CALL	PRTCHR
		DCR		C
		JNZ		NPR000
		RET
		
PRTCHR:	PUSH	H
		PUSH	D
		PUSH	B
		MVI		C,CONIOF
		MOV		E,A
		CALL	BDOS
		POP		B
		POP		D
		POP		H
		RET
		
CLS:	LXI		H,DCLS		; CLEAR THE SCREEN
		CALL	NPRINT
		LXI		H,DHOME		; SET CURSOR HOME
		CALL	NPRINT
		RET
				
;
; SET THE CURSOR TO 
; LINE IN   D (1-24)
; AND 
; COLUMN IN E (1 TO 80 )
;
SETCUR:
		MOV		A,D
		CALL	BIN2ASC		; 2 CHARACTERS IN BC
		MOV		A,B
		STA		DSLIN
		MOV		A,C
		STA		DSLIN+1
		MOV		A,E
		CALL	BIN2ASC
		MOV		A,B
		STA		DSCOL
		MOV		A,C
		STA		DSCOL+1
		LXI		H,DSETCUR
		CALL	NPRINT
		RET
		
;
; CONVERT BINARY NUMBER IN A TO 
; TWO ASCII DIGITS IN BC
;
BIN2ASC:
		LXI		B,3000H		; DEFAULT ZERO IN B
B2A01:	CPI		10			; BIGGER THAN 10?
		JC		B2A02		; NO
		SUI		10
		INR		B
		JMP		B2A01
B2A02:	ADI		30H			; REST IN C
		MOV		C,A
		RET

PRTBORD:
		CALL	CLS
		LXI		B,0000H		
PRTB01:
		LXI		H,LINPOS
		DAD		B
		MOV		D,M
		LXI		H,COLPOS
		DAD		B
		MOV		E,M
		PUSH	B
		CALL	SETCUR
		POP		B
		LXI		H,BORD
		DAD		B
		MOV		A,M
		CALL	PRTCHR
		INR		C
		MVI		A,33
		CMP		C
		JNZ		PRTB01
		LXI		H,DFOOT
		CALL	NPRINT
;
		MVI		B,'1'
		MVI		C,7
		LXI		D,0101H
PRTB02:	PUSH	B
		CALL	SETCUR
		POP		B
		MOV		A,B
		CALL	PRTCHR
		INR		B
		INR		D
		DCR		C
		JNZ		PRTB02
		LXI		D,0A01H		; LINE 20, COL 1
		CALL	SETCUR
		RET		
	
;
; RETURNS ZERO IF OK
; ELSE RETURNS NZ
;
ISMOVEOK:
		LXI		B,0000H
		LXI		D,0000H
		LDA		MOVEPTR
		MOV		C,A
		LXI		H,JUMP
		DAD		B		; HL POINTS TO JUMP
		MOV 	E,M
		LXI		H,BORD
		DAD		D
		MOV		A,M
		CPI		PWN
		RNZ
		LXI		H,OVER
		DAD		B
		MOV		E,M
		LXI		H,BORD
		DAD		D
		MOV		A,M
		CPI		PWN
		RNZ
		LXI		H,LAND
		DAD		B
		MOV		E,M
		LXI		H,BORD
		DAD		D
		XRA		A
		MOV		A,M
		CPI		HOL
		RET
		
DOMOVE:	
		LXI		B,0000H
		LXI		D,0000H
		LDA		MOVEPTR
		MOV		C,A			; BC HOLDS MOVEPTR
;
		LXI		H,JUMP
		DAD		B
		MOV		E,M			; DE HOLDS POINTER TO PAWN
		PUSH	D
		PUSH	B
		CALL	SETSCRPOS	; SCREEN POS IN DE
		MVI		A,HOL
		CALL	PRTCHR
		POP		B		
		POP		D
		LXI		H,BORD
		DAD		D
		MVI		M,HOL
;
		LXI		H,OVER
		DAD		B
		MOV		E,M			; DE HOLD POINTER TO PAWN
		PUSH	D
		PUSH	B
		CALL	SETSCRPOS
		MVI		A,HOL
		CALL	PRTCHR
		POP		B
		POP		D
		LXI		H,BORD
		DAD		D
		MVI		M,HOL
;
		LXI		H,LAND
		DAD		B
		MOV		E,M			; DE HOLDS POINTER TO HOL
		PUSH	D
		PUSH	B
		CALL	SETSCRPOS
		MVI		A,PWN
		CALL	PRTCHR
		POP		B
		POP		D
		LXI		H,BORD
		DAD		D
		MVI		M,PWN
;
		LXI		H,MOVEBUF
		LDA		MOVENO
		MOV		E,A
		DAD		D
		LDA		MOVEPTR
		MOV		M,A
;
		CALL	DISMOV
		LDA		MOVENO		; POINT TO NEXT MOVE TO MAKE
		INR		A
		STA		MOVENO
		CPI		31			; 31 MOVES ?
		RNZ					; NO CONTINUE
;
; DONE !
;		
		LDA		BORD+16		; LAST PAWN IN CENTER ?
		CPI		PWN
		RNZ					; NO CONTINUE
		LXI		D,1401H		; LINE 20 COL 1
		CALL	SETCUR
		JP		RBOOT		
		
SETSCRPOS:
		PUSH	D
		POP		B
		LXI		H,LINPOS
		DAD		B
		MOV		D,M
		LXI		H,COLPOS
		DAD		B
		MOV		E,M
		CALL	SETCUR
		RET

UNMOVE:
		XRA		A
		MOV		B,A
		MOV		D,A
		LDA		MOVEPTR
		MOV		C,A
;
		LXI		H,JUMP
		DAD		B
		MOV		E,M			; DE HOLDS POINTR TO HOL
		PUSH	D
		PUSH	B
		CALL	SETSCRPOS	; SCREEN POS IN DE
		MVI		A,PWN
		CALL	PRTCHR
		POP		B		
		POP		D
		LXI		H,BORD
		DAD		D
		MVI		M,PWN
;
		LXI		H,OVER
		DAD		B
		MOV		E,M			; DE HOLD POINTR TO HOL
		PUSH	D
		PUSH	B
		CALL	SETSCRPOS
		MVI		A,PWN
		CALL	PRTCHR
		POP		B
		POP		D
		LXI		H,BORD
		DAD		D
		MVI		M,PWN
;
		LXI		H,LAND
		DAD		B
		MOV		E,M			; DE HOLDS POINTR TO PWN
		PUSH	D
		PUSH	B
		CALL	SETSCRPOS
		MVI		A,HOL
		CALL	PRTCHR
		POP		B
		POP		D
		LXI		H,BORD
		DAD		D
		MVI		M,HOL
;
		LDA		MOVENO		; DECREMENT NUMBER OF MOVES
		DCR		A
		STA		MOVENO
		RET

FINDMOVE:
		LDA		MOVEPTR
FM00:	CPI		MOVL
		JNC		FM01
		CALL	ISMOVEOK
		RZ
		LDA		MOVEPTR
		INR		A
		STA		MOVEPTR
		JMP		FM00
FM01:	XRA		A
		SUI		1		; SET NZFLAG
		RET
			
				
BORD:	DB				PWN,PWN,PWN
		DB				PWN,PWN,PWN
		DB		PWN,PWN,PWN,PWN,PWN,PWN,PWN
		DB		PWN,PWN,PWN,HOL,PWN,PWN,PWN
		DB		PWN,PWN,PWN,PWN,PWN,PWN,PWN
		DB				PWN,PWN,PWN
		DB				PWN,PWN,PWN

LINPOS:	DB				  1,  1,  1
		DB		          2,  2,  2
		DB		  3,  3,  3,  3,  3,  3,  3
		DB        4,  4,  4,  4,  4,  4,  4
		DB        5,  5,  5,  5,  5,  5,  5
		DB                6,  6,  6
		DB                7,  7,  7

BORL:	EQU		$-LINPOS-1
		
COLPOS:	DB                7,  9, 11
		DB                7,  9, 11
		DB		  3,  5,  7,  9, 11, 13, 15
		DB        3,  5,  7,  9, 11, 13, 15
		DB        3,  5,  7,  9, 11, 13, 15
		DB                7,  9, 11
		DB                7,  9, 11
		
JUMP:	DB	0,0
		DB	1
		DB	2,2
		DB	3,3
		DB	4
		DB	5,5
		DB	6,6
		DB	7,7
		DB	8,8,8,8
		DB	9,9,9,9
		DB	10,10,10,10
		DB	11,11
		DB	12,12
		DB	13
		DB	14
		DB	15,15,15,15
		DB	16,16,16,16
		DB	17,17,17,17
		DB	18
		DB	19
		DB	20,20
		DB	21,21
		DB	22,22,22,22
		DB	23,23,23,23
		DB	24,24,24,24
		DB	25,25
		DB	26,26
		DB	27,27
		DB	28
		DB	29,29
		DB	30,30
		DB	31
		DB	32,32

MOVL:	EQU		$-JUMP-1
		
OVER:	DB	3,1
		DB	4
		DB	5,1
		DB	8,4
		DB	9
		DB	10,4
		DB	13,7
		DB	14,8
		DB	15,7,3,9
		DB	16,8,4,10
		DB	17,9,5,11
		DB	18,10
		DB	19,11
		DB	14
		DB	15
		DB	22,14,8,16
		DB	23,15,9,17
		DB	24,16,10,18
		DB	17
		DB	18
		DB	13,21
		DB	14,22
		DB	27,21,15,23
		DB	28,22,16,24
		DB	29,23,17,25
		DB	24,18
		DB	25,19
		DB	22,28
		DB	23
		DB	28,24
		DB	27,31
		DB	28
		db	31,29
		
LAND:	DB	8,2
		DB	9
		DB	10,0
		DB	15,5
		DB	16
		DB	17,3
		DB	20,8
		DB	21,9
		DB	22,6,0,10
		DB	23,7,1,11
		DB	24,8,2,12
		DB	25,9
		DB	26,10
		DB	15
		DB	16
		DB	27,13,3,17
		DB	28,14,4,18
		DB	29,15,5,19
		DB	16
		DB	17
		DB	6,22
		DB	7,23
		DB	30,20,8,24
		DB	31,21,9,25
		DB	32,22,10,26
		DB	23,11
		DB	24,12
		DB	15,29
		DB	16
		DB	27,17
		DB	22,32
		DB	23
		DB	30,24
		
;		
MOVEPTR:
		DB 	0
;		
MOVENO:	DB	0		
;		
MOVEBUF:
		DS	33
;
DFOOT:	DB		DF01,LF,CR,'  A B C D E F G',LF,CR
DF01	EQU		$-DFOOT-1
;		
DSETCUR:
		DB		DSETC1,ESC,OPENB
DSLIN:	DB		'0','0',SCOLON
DSCOL:	DB		'0','0','H'
DSETC1	EQU		$-DSETCUR-1		
;		
DCLS:	DB		DCLS1,ESC,OPENB,'2','J'
DCLS1 	EQU		$-DCLS-1
;
DHOME:	DB		DHOME1,ESC,OPENB,'M'
DHOME1	EQU		$-DHOME-1
;
OUTMVBUF:
		DB		OMB01
MTENS:	DB		'0'
MONES:	DB		'0'
		DB		' : '
JLET:	DB		'A'
JNUM:	DB		'1'
		DB		'-'
LLET:	DB		'C'
LNUM:	DB		'3'
OMB01	EQU		$-OUTMVBUF-1


		DS		128
STACK	EQU		$

